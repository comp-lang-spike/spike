#!/usr/bin/env spike

class Color {
    red() { self.green(); }
    green() { self.blue(); }
    blue() { stdout.printf("blue: %s\n", self.printString); }
}

createClass() {
    obj myClass;
    
    myClass = (Color)
              subclass: $Foo
              instVarCount: 4
              classVarCount: 2;
    stdout.printf("created class: %s\n", myClass.printString);
    
    obj myInstance;
    myInstance = myClass.new();
    myInstance.blue();
}

interpreter(argv) {
    obj sourcePathname = argv[1];
    obj stream = FileStream.open(sourcePathname, "r");
    if (stream == null) {
        stderr.printf("%s: cannot open '%s'\n", argv[0], sourcePathname);
        return 1;
    }
    
    obj st = SymbolTable.new();
    obj notifier = Notifier.new(stderr);
    
    obj parser = Parser.new();
    obj tree = parser.parse(stream, st);
    tree = tree.source(sourcePathname);
    tree = tree.asModuleDef();
    tree.check(st, notifier);
    
    obj moduleClass = tree.generateCode();
    obj module = moduleClass.new();
    
    obj newArgv = Array.new(argv.size - 1);
    newArgv[0] = sourcePathname;
    obj i;
    for (i = 1; i < newArgv.size; ++i) {
        newArgv[i] = argv[i + 1];
    }
    
    stdout.printf("---- running %s\n", sourcePathname);
    module._init();
    module.main(newArgv);
    
    return 0;
}

main(argv) {
    createClass();
    if (argv.size > 1)
        return interpreter(argv);
    return 0;
}
